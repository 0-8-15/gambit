; Script generated by the HM NIS Edit Script Wizard.

; HM NIS Edit Wizard helper defines
!define PRODUCT_NAME "@NAME@"
!define PRODUCT_VERSION "@VERSION@"
!define PRODUCT_VARIANT "@VARIANT@"
!define PRODUCT_PUBLISHER "Universite de Montreal"
!define PRODUCT_WEB_SITE "http://www.iro.umontreal.ca/~gambit"
!define PRODUCT_DIR_REGKEY "Software\Microsoft\Windows\CurrentVersion\App Paths\uninstall-gambc.exe"
!define PRODUCT_UNINST_KEY "Software\Microsoft\Windows\CurrentVersion\Uninstall\${PRODUCT_NAME} v${PRODUCT_VERSION}"
!define PRODUCT_UNINST_ROOT_KEY "HKLM"

; MUI 1.67 compatible ------
!include "MUI.nsh"

!include "AddToPath.nsh"

; MUI Settings
!define MUI_ABORTWARNING
!define MUI_ICON "@ICON_FILE@"
!define MUI_UNICON "@UNICON_FILE@"

; Welcome page
!define MUI_WELCOMEPAGE_TEXT "-----------------------------------------------------------------------\r\n\r\nThe Gambit Scheme system is a complete, portable, efficient and reliable implementation of the Scheme programming language.\r\n\r\nFor more information about Gambit and add-on packages please check the Gambit web page at\r\n\r\nhttp://www.iro.umontreal.ca/~gambit\r\n\r\n-----------------------------------------------------------------------\r\n\r\nThis wizard will guide you through the installation of ${PRODUCT_NAME} v${PRODUCT_VERSION} (${PRODUCT_VARIANT} variant).\r\n\r\nClick Next to continue\r\n"
!insertmacro MUI_PAGE_WELCOME

; Directory page
!insertmacro MUI_PAGE_DIRECTORY
; Instfiles page
!insertmacro MUI_PAGE_INSTFILES
; Finish page
!insertmacro MUI_PAGE_FINISH

; Uninstaller pages
!insertmacro MUI_UNPAGE_INSTFILES

; Language files
!insertmacro MUI_LANGUAGE "English"

; MUI end ------

Name "${PRODUCT_NAME} v${PRODUCT_VERSION}"
OutFile "@EXE_FILE@"
InstallDir "$PROGRAMFILES\${PRODUCT_NAME}\"
InstallDirRegKey HKLM "${PRODUCT_DIR_REGKEY}" ""
ShowInstDetails show
ShowUnInstDetails show

Section "MainSection" SEC01

  StrCpy $9 "$INSTDIR"

  SetOverwrite try
  SetOutPath "$INSTDIR"
  File /r "C:\${PRODUCT_NAME}\v${PRODUCT_VERSION}"
  SetOutPath "$INSTDIR\v${PRODUCT_VERSION}\bin"
  File "binpatch.exe"
  SetOutPath "$INSTDIR"

  GetFullPathName /SHORT $1 "$INSTDIR"

  ExecWait '$1\v${PRODUCT_VERSION}\bin\binpatch.exe "@DOS_SOURCE_DIR@/v${PRODUCT_VERSION}" "$1\v${PRODUCT_VERSION}" "$1\v${PRODUCT_VERSION}\bin\gsi.exe" "$1\v${PRODUCT_VERSION}\bin\gsc.exe" "$1\v${PRODUCT_VERSION}\bin\six.exe"' $0

  Push "$1\v${PRODUCT_VERSION}\bin"
  Call AddToPath

  Delete '$INSTDIR\v${PRODUCT_VERSION}\bin\binpatch.exe'

SectionEnd

Section -Post

  WriteUninstaller "$INSTDIR\v${PRODUCT_VERSION}\bin\uninstall-gambc.exe"

  WriteRegStr HKLM "${PRODUCT_DIR_REGKEY}" "" "$INSTDIR\"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "DisplayName" "$(^Name)"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "UninstallString" "$INSTDIR\v${PRODUCT_VERSION}\bin\uninstall-gambc.exe"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "DisplayIcon" "$INSTDIR\v${PRODUCT_VERSION}\bin\uninstall-gambc.exe"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "DisplayVersion" "v${PRODUCT_VERSION}"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "URLInfoAbout" "${PRODUCT_WEB_SITE}"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "Publisher" "${PRODUCT_PUBLISHER}"

SectionEnd

Function un.onInit
  MessageBox MB_ICONQUESTION|MB_YESNO|MB_DEFBUTTON2 "Are you sure you want to completely remove ${PRODUCT_NAME} v${PRODUCT_VERSION} (${PRODUCT_VARIANT} variant) and all of its components?" IDYES +2
  Abort
FunctionEnd

Section "Uninstall"

  ; How come the value of $INSTDIR here is not the same as in the main section?
  GetFullPathName /SHORT $1 "$INSTDIR\..\.."

  IfFileExists "$1\v${PRODUCT_VERSION}\bin\uninstall-gambc.exe" can_be_uninst has_been_uninst

  has_been_uninst:

  MessageBox MB_ICONINFORMATION|MB_OK "${PRODUCT_NAME} v${PRODUCT_VERSION} (${PRODUCT_VARIANT} variant) does not seem to be installed! ($1\v${PRODUCT_VERSION}\bin\uninstall-gambc.exe does not exist)"
  Abort

  can_be_uninst:

  RMDir /r "$1\v${PRODUCT_VERSION}"

  DeleteRegKey ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}"
  DeleteRegKey HKLM "${PRODUCT_DIR_REGKEY}"

  Push "$1\v${PRODUCT_VERSION}\bin"
  Call un.RemoveFromPath

SectionEnd
